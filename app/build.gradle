apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    // RoomのMigrationでスキーマを出力する先
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    defaultConfig {
        applicationId "net.formula97.andorid.car_kei_bo"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 4
        versionName "0.7-p3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // RoomのMigrationでスキーマを出力する先
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":"$projectDir/schemas".toString()]
                // app/schemas/${databaseクラスのFQDN}/$schemaVersion.json が生成されるようになる
            }
        }

    }

    signingConfigs {
        debug {
            storeFile file(System.getenv("DEBUG_KEYSTORE"))
        }
        release {
            storeFile file(System.getenv("RELEASE_KEYSTORE"))
            storePassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    def room_version = "1.1.1"
    def dagger2_version = "2.17"

    implementation 'com.android.support:appcompat-v7:28.0.0-rc01'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Dagger 2 DI
    implementation "com.google.dagger:dagger:$dagger2_version"
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger2_version"

    // Room Persistence Library
    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    testImplementation "android.arch.persistence.room:testing:$room_version"
    kaptTest "android.arch.persistence.room:compiler:$room_version"
    androidTestImplementation "android.arch.persistence.room:testing:$room_version"

}
